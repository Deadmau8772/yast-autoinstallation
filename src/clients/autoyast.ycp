/**
 * File:	clients/autoyast.ycp
 * Summary:	Main file for client call
 * Authors:	Anas Nashif <nashif@suse.de>
 *
 * $Id$
 */
{
    textdomain "autoinst";
    import "Wizard";
    import "Mode";
    Mode::SetMode("autoinst_config");

    import "Profile";
    import "AutoinstConfig";
    import "Y2ModuleConfig";
    import "Label";
    import "Sequencer";
    import "Popup";
    import "AddOnProduct";
    import "CommandLine";
    import "Y2ModuleConfig";
    import "AutoInstall";

    include "autoinstall/dialogs.ycp";
    include "autoinstall/conftree.ycp";
    include "autoinstall/wizards.ycp";


    if (size(Y2ModuleConfig::GroupMap) == 0)
    {
        Wizard::CreateDialog();
        Popup::Error(_("Error while reading configuration data."));
        Wizard::CloseDialog();
        return `abort;
    }

    Pkg::CallbackImportGpgKey(AutoInstall::callbackTrue_boolean_map);
    Pkg::CallbackAcceptUnknownGpgKey(AutoInstall::callbackTrue_boolean_string_string);
    Pkg::CallbackAcceptFileWithoutChecksum(AutoInstall::callbackTrue_boolean_string);
    Pkg::CallbackAcceptUnsignedFile(AutoInstall::callbackTrue_boolean_string);

    define boolean openFile( map <string, string> options ) {
        if( !Profile::ReadXML( options["filename"]:"" ) ) {
            Popup::Error(_("Error while parsing the control file.
Check the log files for more details or fix the
control file and try again.
"));
        }
                        Popup::ShowFeedback(_("Reading configuration data"), _("This may take a while"));
                        foreach(string p, map d, Profile::ModuleMap, ``{
                                /* Set resource name, if not using default value */
                                string resource = d["X-SuSE-YaST-AutoInstResource"]:"";
                                if (resource == "")
                                {
                                    resource = p;
                                }
                                y2debug("resource: %1", resource );
                                string tomerge = d["X-SuSE-YaST-AutoInstMerge"]:"";
                                string module_auto =d["X-SuSE-YaST-AutoInstClient"]:"none";
                                any rd = Y2ModuleConfig::getResourceData(d
                                    ,resource);
                                if (rd!=nil) {
                                WFM::CallFunction(module_auto, ["Import",
                                    rd]);
                                }
                                });
                        Popup::ClearFeedback();
                        AutoSequence ();
                        return true;
    }

    define boolean runModule( map <string, string> options ) {
        AutoinstConfig::runModule = options["modname"]:"";
        AutoSequence ();
        return true;
    }


    map cmdline = $[
        "id"        : "autoyast2",
        "help"      : _("autoyast"),
        "guihandler" : AutoSequence,
        "actions"   : $[
            "file" :   $[ "handler":openFile, "help":"file operations" ],
            "module":  $[ "handler":runModule, "help":"module specific operations" ]
        ],
        "options"       : $[
            "filename": $[
                "type":"string",
                "help":"filename=XML_PROFILE"
            ],
            "modname": $[
                "type":"string",
                "help":"modname=AYAST_MODULE"
                ]
        ],
        "mappings"      : $[
            "file"  :   ["filename"],
            "module" :  ["modname"]
        ]
    ];


    // command line options
    /* Init variables */
    string command = "";
    list flags = [];
    map<string,any> options = $[];
    string exit = "";
    list l = [];

    any ret = nil;
    ret = CommandLine::Run(cmdline);


    AddOnProduct::CleanModeConfigSources();
    return `exit;
}
