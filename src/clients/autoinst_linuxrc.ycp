/**
 * File:	clients/autoinst_linuxrc.ycp
 * Package:	Autoinstallation Configuration System
 * Summary: 	Linuxrc Settings
 * Authors:	Anas Nashif<nashif@suse.de>
 *
 * $Id$
 */
{
    textdomain "autoinst";
    import "Mode";
    Mode::config = true;


    import "Wizard";
    import "Summary";
    import "AutoinstLinuxrc";

    include "wizard/sequencer.ycp";
    include "autoinstall/linuxrc_dialogs.ycp";
    include "ui/common_messages.ycp";
    include "network/ip.ycp";

    list args = WFM::Args ();

    if ( size (args) <= 0 )
    {
	y2error ("Did not get the settings, probably some mistake...");
	return false;
    }
    if ( !is ( WFM::Args (0), map ) )
    {
	y2error ("Bad argument: %1", WFM::Args (0));
	return false;
    }

    map settings = $[];
    {
	integer i = 0;
	while (i < size (WFM::Args()))
	{
	    if (is (WFM::Args (i), map) && nil != WFM::Args (i))	settings = WFM::Args (i);
	    i = i + 1;
	}
    }

    y2milestone("Imported: (%1)", settings);
    AutoinstLinuxrc::Import ( settings );

    define set_contents()
	``{
	term contents =
	    `VBox(
		  `VSpacing(1),
		  `RichText( `id(`summary), AutoinstLinuxrc::Summary()),
		  `VSpacing(0.5),
		  `HBox(
			`PushButton(`id(`configure), _("&Installation Options")),
			`HStretch(),
			`PushButton(`id(`reset), _("&Reset Configuration"))
			),
		  `VSpacing(1)
		  );
	Wizard::SetContents(_("Installation Options"),
			    contents, "", true, true);
    }
    map dialogs = $[
		    "autoyast": 	``(   autoyastOptions() ),
		    "linuxrc"	:	``(   linuxrcOptions() )
    ];


    map sequence = $[
		     "ws_start" 	: "linuxrc",
		     "linuxrc":
		     $[
		       `abort 	: `abort,
		       `next	: "autoyast",
		       `back	: `back
		     ],
		     "autoyast"	:
		     $[
		       `abort 	: `abort,
		       `next	: `next,
		       `back	: "linuxrc"
		     ]
    ];
    

    set_contents();
    any result = nil;
    any ret = nil;
    repeat {
	ret = UI::UserInput();
	if (ret == `configure)
	{
	    
	    Wizard::CreateDialog ();	    
	    result =  WizardSequencer (dialogs, sequence);	    
	    UI::CloseDialog ();

	    if (result == `next || result == `finish )
	    {
		settings = AutoinstLinuxrc::Export ();
	    }
	    set_contents();
	}
	else if ( ret == `reset)
	{
	    settings = $[];
	    AutoinstLinuxrc::Set(settings);
	    set_contents();
	}
    } until (ret == `back || ret == `next || ret ==`key  || ret == `abort);

    return [ret, settings];
}
