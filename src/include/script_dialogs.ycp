/**
 * File:
 *	script_dialogs.ycp
 *
 * Module:
 *	script_dialogs.ycp
 *
 * Summary:
 *	This module handles the configuration for auto-installation
 *
 * Authors:
 *	Anas Nashif <nashif@suse.de>
 *
 * $Id$
 */

{

    textdomain "autoinst";

    /**
     * @return  script configuration dialog
     */
    global define string script_dialog_contents()
	``{
	list allscripts = maplist(`s, AutoinstScripts::merged,
				  ``{
				      return (`item(`id(s["filename"]:"Unknown"),
						    s["filename"]:"Unknown",
						    AutoinstScripts::typeString(s["type"]:""),
						    s["interpreter"]:"Unknown")
					      );
				  });
	term contents =
	    `VBox(
		  `Left(
			`Label(_("Available Scripts"))
			),
		  `Table( `id(`table), `opt(`notify),
			  `header(_("Script Name"), _("Type"), _("Interpreter")),
			  allscripts
			  ),
		  `HBox(
			`PushButton(`id(`new),  Label::NewButton()),
			`PushButton(`id(`edit), Label::EditButton()),
			`PushButton(`id(`delete), Label::DeleteButton())
			)
		  );
	return contents;
    }



    /**
     * Main dialog
     * @return symbol
     */
    global define symbol ScriptsDialog()
    ``{

	string help = _("<p>
By adding scripts to the autoinstallation process, customize the installation for
your needs and take control in different stages of the installation.</p>
");

	string title = _("User Script Management");
	Wizard::SetContents(title,
			    script_dialog_contents(), help, true, true);

	Wizard::ReplaceAbortButton(`Empty());
	UI::ChangeWidget(`id(`next), `Label, Label::FinishButton());
	any ret = nil;
	repeat {
	    ret = UI::UserInput();

	    if (ret == `new)
	    {
		Wizard::CreateDialog();
		ScriptDialog(ret , "" );
		Wizard::CloseDialog();
	    }
	    else if (ret == `edit)
	    {
		string name = UI::QueryWidget(`id(`table), `CurrentItem);
		if (name != nil)
		{
		    Wizard::CreateDialog();
		    ScriptDialog(ret , name  );
		    Wizard::CloseDialog();
		}
		else
		{
		    Popup::Message(_("Select a script first."));
		    continue;
		}

	    }
	    else if (ret == `delete)
	    {
		string name = UI::QueryWidget(`id(`table), `CurrentItem);
		if (name != nil) {
		    AutoinstScripts::deleteScript(name);

		}
		else
		{
		    Popup::Message(_("Select a script first."));
		    continue;
		}
	    }
	    Wizard::SetContents(title,
			    script_dialog_contents(), help, true, true);


	} until (ret == `next || ret == `back  );

	return ret;
    }



    /**
     * Dialog for adding/Editing  a script
     * @param mode `edit or `new
     * @param name script name
     * @return symbol
     */

    global define ScriptDialog(symbol mode , string name)
    ``{
	map script = $[];
	if (mode == `edit)
	{
	    list filtered_scripts = filter(`s, AutoinstScripts::merged,``((lookup(s,"filename","") == name)));
	    if (size(filtered_scripts)>0)
	    {
		script = select(filtered_scripts,0,$[]);
	    }
	}

	// help 1/4
	string help = _("
<h3>Pre-Scripts</h3>
<P>Add commands to run on the system  before the installation begins. </P>
<h3>Post-Scripts</h3>
");
	// help 2/4
	help = help + _("
<P>You can also add commands to execute on the system after the installation
is completed. These scripts are run outside the chroot environment.
</P>");
	// help 3/4
	help = help + _("
<H3>Chroot Environment</H3>
<P>For your postinstallation script to run inside the chroot
environment, choose the <i>chroot scripts</i> options.
</P>
");

	// help 4/4
	help = help + _("
<H3>Interpreter:</H3>
<P>Pre-scripts can only be shell scripts. Do not use <i>Perl</i> or <i>Python</i> for pre-scripts.
</P>
");	
	string title = _("Script Editor");

	term contents=
	    `VBox(
		  `HBox(
			`TextEntry(`id(`filename), _("&File Name"), lookup(script,"filename","")),
			`ComboBox( `id(`interpreter), _("&Interpreter"),
				   [
				    `item(`id("perl"), _("Perl"),(script["interpreter"]:"shell" == "perl")),
				    `item(`id("shell"), _("Shell"),(script["interpreter"]:"shell" == "shell")),
				    `item(`id("shell"), _("Python"),(script["interpreter"]:"shell" == "python"))
				    ]
				   ),
			`ComboBox( `id(`type), _("&Type"),
				   [
				    `item(`id("pre-scripts"), _("Pre"), (script["type"]:""  == "pre-scripts")),
				    `item(`id("post-scripts"), _("Post"),(script["type"]:"" == "post-scripts")),
				    `item(`id("chroot-scripts"), _("Chroot"),(script["type"]:"" == "chroot-scripts"))
				    ]
				   ),
			`HStretch(),
			`Empty()
			),
		  `VSpacing(1),
		  `HBox(
			`MultiLineEdit(`id(`source), _("S&cript Source"), script["source"]:"")
			),
		  `VSpacing(1),
		  `HBox(
			`PushButton(`id(`save), Label::SaveButton()),
			`PushButton(`id(`loadsource), _("&Load new source")),
			`PushButton(`id(`cancel), Label::CancelButton())

			)
		  );

	Wizard::ReplaceButtonBox(`VSpacing(0.6));
	Wizard::SetContents(title,
			    contents, help, true, true);


	if (mode == `edit)
	{
	    UI::ChangeWidget(`id(`filename), `Enabled, false);
	}

	any ret = nil;
	repeat {
	    ret = UI::UserInput();
	    if (ret == `save) {
		string scriptName = UI::QueryWidget(`id(`filename), `Value);

		string type = UI::QueryWidget(`id(`type), `Value);
		string interpreter = UI::QueryWidget(`id(`interpreter), `Value);
		string source = UI::QueryWidget(`id(`source), `Value);

		if (source == "" || scriptName == "")
		{
		    Popup::Message(_("Provide at least the script
name and the content of the script.
"));
		    continue;
		}
		else
		{
		    y2debug("Calling ConfigureScripts: %1, %2, %3, %4", scriptName, source, interpreter, type);
		    AutoinstScripts::AddEditScript( scriptName, source, interpreter, type);
		}
	    }
	    else if (ret == `loadsource)
	    {
		string filename = UI::AskForExistingFile( AutoinstConfig::Repository, "*.*", _("Select a file to load"));
		if (filename != "")
		{
		    string source = SCR::Read(.target.string, filename);
		    UI::ChangeWidget(`id(`source), `Value, source);
		    continue;
		}
	    }

	} until (ret == `save || ret == `cancel || ret == `back);
	return ret;

    }





}
