/**
 * File:	include/autoinstall/general_dialogs.ycp
 * Module:	Environment
 * Summary:	Handle environment data
 * Authors:	Anas Nashif <nashif@suse.de>
 *
 * $Id$
 */
{
    textdomain "autoinst";


    /**
     * Auto Sequence
     * @return symbol
     */

    global define symbol linuxrcAutoSequence() ``{
	map dialogs = $[
			"autoyast":	``(   autoyastOptions() ),
			"linuxrc"	:	``(   linuxrcOptions() )
	];


	map sequence = $[
			 "ws_start"	: "linuxrc",
			 "linuxrc":
			 $[
			   `abort	: `abort,
			   `next	: "autoyast",
			   `back	: `back
			 ],
			 "autoyast"	:
			 $[
			   `abort	: `abort,
			   `next	: `next,
			   `back	: "linuxrc"
			 ]
	];
	Wizard::CreateDialog ();
	
	Wizard::ReplaceAbortButton(`Empty());
	any result =  WizardSequencer (dialogs, sequence);
	UI::CloseDialog ();

	return result;
    }


    /**
     * Linxurc Options
     */

    global define linuxrcOptions()``{


	string method = eval(AutoinstLinuxrc::method);	
	
	map instMethods = $[
			     "nfs"	: _("NFS"),
			     "hd"	: _("Hard drive"),
			     "cd"	: _("CDROM"),
			     "smb"	: _("Samba"),
			     "ftp"	: _("FTP"),
			     "http"	: _("HTTP")
	];


	list methods = maplist(`k,`v, instMethods, ``{
	    return(`item(`id(k), v, (method == k)));
	});

	string help_text = _("<P>The information entered in this and the following dialogs will be used
in the early boot process to determine the source of the installation and the initial system
settings, such as the network and system environment.</P>
");

	term isPoint =  `VBox(			     
			      `HBox(
				    `HWeight(30,`TextEntry(`id(`server), `opt(`notify), _("&Server"), AutoinstLinuxrc::server)),
				    `HWeight(10,`TextEntry(`id(`port), `opt(`notify), _("P&ort"), AutoinstLinuxrc::port  )),
				    `HWeight(35,`TextEntry(`id(`serverdir), `opt(`notify), _("Server &Directory"), AutoinstLinuxrc::serverdir))
			      )
			      );
	term smbPoint = `HBox(			     
			      `HWeight(30,`TextEntry(`id(`workdomain), `opt(`notify), _("&Domain"), AutoinstLinuxrc::workdomain)),
			      `HWeight(40, `Empty())
			      );
	term hdPoint = `HBox(
			     
			      `HWeight(30,`TextEntry(`id(`partition), `opt(`notify), _("&Partition"), AutoinstLinuxrc::partition)),
			      `HWeight(40, `Empty())
			      );
	term contents =
	    `Top(
		 `VBox(
		       `Frame(_("Installation Source"),
			      `VBox(
				    `Left(`ComboBox(`id(`method), `opt(`notify), _("&Method"),
					      methods
					      )
					  ),
				    `ReplacePoint(`id(`is), isPoint )
				    )
			      ),
		       `VSpacing(0.5),
		     
		       `Frame(_("Other Settings"), 
			      `HBox(
				   
				    `CheckBox(`id(`usedhcp), `opt(`notify), _("Use DHCP") , AutoinstLinuxrc::usedhcp),
				    `CheckBox(`id(`textmode), _("Start in text mode"), AutoinstLinuxrc::textmode),
				    `HStretch()
				    )
			      ),
		       `VSpacing(0.5),
		       `Frame(_("Client Network Settings"),
			      `VBox(
				   `Left(`ComboBox(`id(`netdevice), `opt(`notify), _("Net de&vice"),
						   ["eth0", "eth1", "eth2"]
						   )
					 ),
				   `HBox(					 				    
					 `TextEntry(`id(`ip), `opt(`notify), _("&IP"), AutoinstLinuxrc::ip),
					 `TextEntry(`id(`netmask), `opt(`notify), _("Netmas&k"), AutoinstLinuxrc::netmask  ),
					 `TextEntry(`id(`gateway), `opt(`notify), _("Gatewa&y"), AutoinstLinuxrc::gateway)
					 ),
				   `HBox(
					 `TextEntry(`id(`nameserver), `opt(`notify), _("Name server"), AutoinstLinuxrc::nameserver),
					 `TextEntry(`id(`domain), `opt(`notify), _("Domain"), AutoinstLinuxrc::domain  )				  
					 )
				   )
			      )      		       
		       )
		 );

	
	Wizard::SetContents(_("Installation Options"),
			    contents, help_text, true , true);

	any ret = nil;
	
	repeat {
	   
	    method = UI::QueryWidget(`id(`method), `Value);	    
	  


	    
	    if ( method != "smb" && method != "hd" &&( UI::WidgetExists(`id(`workdomain)) ||  UI::WidgetExists(`id(`partition)) ) ) {
		UI::ReplaceWidget(`id(`is), isPoint);
	    }
	    
	    if ( method == "cd") {		
		UI::ChangeWidget(`id(`server), `Enabled, false);
		UI::ChangeWidget(`id(`serverdir), `Enabled, false);
		UI::ChangeWidget(`id(`port), `Enabled, false);		
	    }
	    else if ( method == "smb") {		
		UI::ReplaceWidget(`id(`is), smbPoint);
	    }
	    else if ( method == "hd") {		
		UI::ReplaceWidget(`id(`is), hdPoint);
	    }
	    else if  ( method == "nfs") {
		UI::ChangeWidget(`id(`port), `Enabled, false);
		UI::ChangeWidget(`id(`server), `Enabled, true);
		UI::ChangeWidget(`id(`serverdir), `Enabled, true);
	    }
	    else if  ( method == "ftp") {
		UI::ChangeWidget(`id(`port), `Enabled, false);
		UI::ChangeWidget(`id(`server), `Enabled, true);
		UI::ChangeWidget(`id(`serverdir), `Enabled, true);
	    } else {
		UI::ChangeWidget(`id(`server), `Enabled, true);
		UI::ChangeWidget(`id(`serverdir), `Enabled, true);
		UI::ChangeWidget(`id(`port), `Enabled, true);	
	    }

	    if (UI::QueryWidget(`id(`usedhcp), `Value)) {
		UI::ChangeWidget(`id(`ip), `Enabled, false);
		UI::ChangeWidget(`id(`netmask), `Enabled, false);
		UI::ChangeWidget(`id(`gateway), `Enabled, false);
		UI::ChangeWidget(`id(`nameserver), `Enabled, false);
		UI::ChangeWidget(`id(`domain), `Enabled, false);
	    } else {
		UI::ChangeWidget(`id(`ip), `Enabled, true);
		UI::ChangeWidget(`id(`netmask), `Enabled, true);
		UI::ChangeWidget(`id(`gateway), `Enabled, true);
		UI::ChangeWidget(`id(`nameserver), `Enabled, true);
		UI::ChangeWidget(`id(`domain), `Enabled, true);		
	    }
		
	    ret = UI::UserInput();
	    if (ret == `next) {
		if ( method != "smb" && method != "hd" && method!="cd") {
		    
		    if(!check_ip4(UI::QueryWidget(`id(`server), `Value))) {
			UI::ErrorPopup(_("The IP address is incorrect"));
			UI::SetFocus(`id(`server));
			continue;
		    }
		    AutoinstLinuxrc::server = UI::QueryWidget(`id(`server), `Value);
		    
		    AutoinstLinuxrc::serverdir = UI::QueryWidget(`id(`serverdir), `Value);
		    AutoinstLinuxrc::port = UI::QueryWidget(`id(`port), `Value);
		  
		}
		AutoinstLinuxrc::usedhcp = UI::QueryWidget(`id(`usedhcp), `Value);
		AutoinstLinuxrc::textmode = UI::QueryWidget(`id(`textmode), `Value);
		
		if (!UI::QueryWidget(`id(`usedhcp), `Value)) {
		    if(!check_ip4(UI::QueryWidget(`id(`ip), `Value))  && UI::QueryWidget(`id(`ip), `Value) != "") {
			UI::ErrorPopup(_("The IP address is incorrect"));
			UI::SetFocus(`id(`ip));
			continue;
		    }
		    if(!check_netmask(UI::QueryWidget(`id(`netmask), `Value)) && UI::QueryWidget(`id(`netmask), `Value) != "" ) {
			UI::ErrorPopup(_("The subnet mask is incorrect"));
			UI::SetFocus(`id(`netmask));
			continue;
		    }
		    if(!check_ip4(UI::QueryWidget(`id(`gateway), `Value)) && UI::QueryWidget(`id(`gateway), `Value) != "") {
			UI::ErrorPopup(_("The IP address is incorrect"));
			UI::SetFocus(`id(`gateway));
			continue;
		    }
		    if(!check_ip4(UI::QueryWidget(`id(`nameserver), `Value)) && UI::QueryWidget(`id(`nameserver), `Value) != "" ) {
			UI::ErrorPopup(_("The IP address is incorrect"));
			UI::SetFocus(`id(`nameserver));
			continue;
		    }
		    
		    AutoinstLinuxrc::ip = UI::QueryWidget(`id(`ip), `Value);
		    AutoinstLinuxrc::netmask = UI::QueryWidget(`id(`netmask), `Value);
		    AutoinstLinuxrc::gateway = UI::QueryWidget(`id(`gateway), `Value);
		    AutoinstLinuxrc::nameserver = UI::QueryWidget(`id(`nameserver), `Value);
		    AutoinstLinuxrc::domain = UI::QueryWidget(`id(`domain), `Value);    
		}
		
		AutoinstLinuxrc::netdevice = UI::QueryWidget(`id(`netdevice), `Value);
		AutoinstLinuxrc::method = UI::QueryWidget(`id(`method), `Value);
		if (UI::WidgetExists(`id(`workdomain))) {
		    AutoinstLinuxrc::method = UI::QueryWidget(`id(`method), `Value);
		}
		if (UI::WidgetExists(`id(`partition))) {
		    AutoinstLinuxrc::partition = UI::QueryWidget(`id(`partition), `Value);
		}
		
	    }
	    
	} until (ret == `next || ret == `back || ret ==`abort);

	return ret;
    }


    /**
     * Autoyast Options
     *
     */ 
    global define autoyastOptions ()``{
	
	string aymethod = eval(AutoinstLinuxrc::aymethod);
	map ayMethods = $[
			  "nfs"		: _("NFS"),
			  "file"	: _("File"),			   
			  "tftp"	: _("TFTP"),
			  "http"	: _("HTTP"),
			  "ftp"		: _("FTP")
	];			


    	list aymethods = maplist(`k,`v, ayMethods, ``{
	    return(`item(`id(k), v, (aymethod == k)));
	});
	string help_text = _("<P>
Use this dialog to enter the location of the <em>control file</em>. The <em>control file</em> can be
located on the media directly accessible to the system (i.e., floppy, CD) or on the network and can
be retrieved using several methods (HTTP, NFS, TFTP or FTP).</p>
");
	term contents =
	    `Top(
		 `VBox(		      
		       `Frame(_("Profile Location"), 
			      `VBox(				    
				    `Left(`ComboBox(`id(`aymethod), `opt(`notify), _("Me&thod"),
						    aymethods
						    )
					  ),
				    `HBox(
				    
					  `HWeight(30,`TextEntry(`id(`ayserver), `opt(`notify), _("S&erver"), AutoinstLinuxrc::ayserver)),
					  `HWeight(10,`TextEntry(`id(`ayport), `opt(`notify), _("&Port"), AutoinstLinuxrc::ayport  )),
					  `HWeight(35,`TextEntry(`id(`ayserverdir), `opt(`notify), _("Profile &Location"), AutoinstLinuxrc::ayserverdir))
					  )
				    )
			      )
		       )
		 );
	
    	Wizard::SetContents(_("Autoyast Options"),
			    contents, help_text, true , true);

	UI::ChangeWidget(`id(`next), `Label, FinishButtonLabel());
	any ret = nil;
	
	repeat {
	    aymethod = UI::QueryWidget(`id(`aymethod), `Value);
	   
	    if ( aymethod == "file") {		
		UI::ChangeWidget(`id(`ayserver), `Enabled, false);	
		UI::ChangeWidget(`id(`ayport), `Enabled, false);		
	    }
	    else if  ( aymethod == "nfs") {
		UI::ChangeWidget(`id(`ayport), `Enabled, false);
	    }
	    else if  ( aymethod == "tftp") {
		UI::ChangeWidget(`id(`ayport), `Enabled, false);	
	    } else {
		UI::ChangeWidget(`id(`ayserver), `Enabled, true);		
		UI::ChangeWidget(`id(`ayport), `Enabled, true);	
	    }
	    		
	    ret = UI::UserInput();

	    if (ret == `next) {
		
		if(!check_ip4(UI::QueryWidget(`id(`ayserver), `Value)) && aymethod != "file") {
		    UI::ErrorPopup(_("The IP address is incorrect"));
		    UI::SetFocus(`id(`server));
		    continue;
		}
		AutoinstLinuxrc::ayserver = UI::QueryWidget(`id(`ayserver), `Value);
		    
		AutoinstLinuxrc::ayserverdir = UI::QueryWidget(`id(`ayserverdir), `Value);
		AutoinstLinuxrc::ayport = UI::QueryWidget(`id(`ayport), `Value);
		AutoinstLinuxrc::aymethod = UI::QueryWidget(`id(`aymethod), `Value);
		break;
	    }
	    
	} until (ret == `next || ret == `back || ret ==`abort);

	return ret;
    }
}
