/**
 * File:	include/autoinstall/bootloader_dialogs.ycp
 * Package:	Autoinstallation Configuration System
 * Summary:	Bootloader
 * Authors:	Anas Nashif<nashif@suse.de>
 *
 * $Id$
 */
{
    textdomain "autoinst";
    import "AIBootloader";
    import "Wizard";

    list args = WFM::Args ();

    if ( size (args) <= 0 )
    {
	y2error ("Did not get the settings, probably some mistake...");
	return false;
    }
    if ( !is ( WFM::Args (0), map ) )
    {
	y2error ("Bad argument: %1", WFM::Args (0));
	return false;
    }

    map settings = $[];
    {
	integer i = 0;
	while (i < size (WFM::Args()))
	{
	    if (is (WFM::Args (i), map) && nil != WFM::Args (i))	settings = WFM::Args (i);
	    i = i + 1;
	}
    }

    y2milestone("Imported: (%1)", settings);
    AIBootloader::Import ( settings );
    string caption = _("Bootloader Configuration");


    
    /**
     * Configure Bootloader Options
     * @param void
     * @return symbol
     */

    define symbol Bootloader()
	``{		
	string boot_parameters = AIBootloader::Settings["kernel_parameters"]:"";
	string loc = AIBootloader::Settings["location"]:"none";
       
	term buttons = `VBox(
			     `VStretch(),
			     `Left(
				   `RadioButton( `id( "none" ), `opt(`notify),
						 _("&Do not configure the bootloader (Done automatically)"), loc == "none")
				   ),
			     `Left(
				   `RadioButton( `id( "mbr" ), `opt(`notify),
						 _("&Write LILO to the boot disk ('&MBR')"), loc == "mbr")
				   )
			     );
    
	buttons = add ( buttons,
			`Left(
			      `RadioButton( `id( "floppy" ),`opt(`notify),
					    _("Create a boot &floppy"),
					    loc == "floppy")
			      )
			);

	buttons = add ( buttons,
			`Left(
			      `RadioButton( `id("boot"),`opt(`notify),
					    _("Do &not use LILO (a different boot manager is required)"), loc == "other")
			      )
			);

	string default_text = "";
	boolean custom = false;
	if (substring (loc, 0, 5) == "/dev/")
	{
	    default_text = loc;
	    custom = true;
	}

	buttons = add ( buttons,
			`VSquash(
				 `HBox(
				       `Left(
					     `Bottom(
						     `RadioButton( `id("custom"),`opt(`notify),
								   _("Write LILO to a  &partition:"), custom
								   )
						     )
					     ),
				       `HWeight(1,
						`Bottom(
							`TextEntry( `id(`custom_partition), "", default_text)
							)
						)
				       )
				 )
			);

    
	// Input field for boot parameters
    
	buttons = add ( buttons, `VStretch() );
	
	buttons = add ( buttons,
			`HBox(
			      `HSpacing(4),
			      `HWeight(75, `VCenter(
						    `VBox(
							  `Left(`TextEntry(
									   `id( `boot_params ),
									   _("&Kernel boot parameters:"),
									   boot_parameters
									   )
								),
							  `VSpacing(0.8),
							  `Left(`CheckBox(
									  `id( `linear ),
									  _("Use the '&linear' option"), AIBootloader::Settings["linear"]:false
									  )
								),
							  `VSpacing(0.8),
							  `Left(`CheckBox(
									  `id( `lba ),
									  _("LBA Sup&port"), AIBootloader::Settings["lba_support"]:false
									  )
								),
							  `VSpacing(0.8),
							  `Left(`CheckBox(
									  `id( `activate ),
									  _("Ac&tivate LILO partition"), AIBootloader::Settings["activate"]:false
									  )
								)
							  )
						    )
				       ),
			      `HWeight(25, `HStretch() )
			      )
			);
    
	buttons = add ( buttons, `VStretch() );

	// determine  location of lilo boot loader
	term contents = `Top(`RadioButtonGroup(`id(`options), buttons) ) ;
    //---------------------------------------------------------------

    string helptext = _("<p>
The boot manager can be installed in the following ways:
</p>");

    // custom bootloader help text, 2 of 9
    helptext = helptext + _("<p>
- In the <b>MBR</b> (the Master Boot Record).  This is recommended whenever SuSE Linux
determines the system can be booted this way. The old MBR
will be saved to disk as a precaution.
</p>
");


    helptext = helptext + _("<p>
- In the <b>/boot</b> partition.  Choose this option if you
have installed several operating systems on your hard drive and want to
continue using your old boot manager, such as <tt>loadlin</tt>. No changes
will be made to the existing MBR.<br>
The boot manager is always installed on the <b>/boot</b> partition.
Configure your <b>other boot manager</b> to start SuSE Linux.
</p>
");

    // custom bootloader help text, 5 of 9
    helptext = helptext + _("<p>
If you use this method, configure
your other boot manager (e.g., <tt>loadlin</tt>) accordingly.
</p>
");

    // custom bootloader help text, 6 of 9
    helptext = helptext + _("<p>
- In some <b>other</b> partition. Consider your system's restrictions
when selecting this option.</p>
");
    helptext = helptext + _("<p>For example, most PCs have a BIOS limit that restricts booting to
hard disk cylinders smaller than 1024. Depending on the boot manager used,
you may or may not be able to boot from a logical partition.
</p>");

    // custom bootloader help text, 8 of 9
    helptext = helptext + _("<p>
Enter the device name of the partition (e.g., <tt>/dev/hda3</tt>,
<tt>/dev/sdb</tt>) in the input field.
</p>
");

    // custom bootloader help text, 9 of 9
    helptext = helptext + _("<p>
If your system requires additional <b>kernel boot parameters</b>,
enter them in the corresponding input field. Most users will not need this.
</p>");


			 
	Wizard::SetContents(caption,
			    contents, helptext, true, true);

	any ret = "";
	boolean has_error = false;
	repeat {
	    ret = UI::UserInput();
					
	    if (ret ==`next && !has_error)
	    {
		any option = UI::QueryWidget(`id(`options), `CurrentButton);
		if (option == nil)
		{
		   
		    // User MUST choose one of the options
		    UI::MessagePopup(_("You must choose one of the
options to continue.
"));
		    has_error = true;

		    UI::ChangeWidget(`id(`key), `CurrentItem, "bootloader");
		    continue;
       
		}
		else if (is(option,string))
		{
		    if (option == "mbr")
		    {
			AIBootloader::Settings["location"] =  option;
		    }
		    else if (option == "floppy")
		    {
			AIBootloader::Settings["location"] = option;
		    }
		    else if (option == "boot")
		    {
			AIBootloader::Settings["location"] = "other";
		    }
		    else if (option == "custom")
		    {
			option = UI::QueryWidget(`id(`custom_partition), `Value);
			if ((ret == `key)
			    && ((!is(option, string))
				|| (option == "")
				|| (substring (option, 0, 5) != "/dev/")))
			{
			    UI::MessagePopup(_("You did not enter a partition!"));
			    has_error = true;
			    UI::ChangeWidget(`id(`key), `CurrentItem, "bootloader");
			    continue;
			}
			if (is(option,string))
			{
			    AIBootloader::Settings["location"]= option;
			}     
		    }
		}
		has_error = false;
		option = UI::QueryWidget(`id(`boot_params), `Value);
		if (is (option, string))
		{
		    AIBootloader::Settings["kernel_parameters"]=option;
		}
		option = UI::QueryWidget(`id(`linear), `Value);
		if (is (option, boolean))
		{
		    AIBootloader::Settings["linear"] =option;
		}
		option = UI::QueryWidget(`id(`activate), `Value);
		if (is (option, boolean))
		{
		    AIBootloader::Settings["activate"] = option;
		}
		option = UI::QueryWidget(`id(`lba), `Value);
		if (is (option, boolean))
		{
		    AIBootloader::Settings["lba_support"] = option;
		}
	    }	  		
	} until (ret == `next || ret == `abort  || ret == `back);
	
	return ret;	
    }
    
    define set_contents()
	``{
	term contents =
	    `VBox(
		  `VSpacing(1),
		  `RichText( `id(`summary), AIBootloader::Summary()),
		  `VSpacing(0.5),
		  `HBox(
			`PushButton(`id(`configure), _("&Configure")),
			`HStretch(),
			`PushButton(`id(`reset), _("&Reset Configuration"))
			),
		  `VSpacing(1)
		  );
	Wizard::SetContents(caption,
			    contents, "", true, true);
    }

    set_contents();
    any result = nil;
    any ret = nil;
    repeat {
	ret = UI::UserInput();
	if (ret == `configure)
	{
	        Wizard::CreateDialog ();		
		result =  Bootloader();
		UI::CloseDialog ();

		if (result == `next || result == `finish)
		{
		    settings = AIBootloader::Export ();
		}
		AIBootloader::Set(settings);
		set_contents();
	}
	else if ( ret == `reset)
	{
	    settings = $[];
	    AIBootloader::Set(settings);
	    set_contents();
	}
    } until (ret == `back || ret == `next || ret ==`key  || ret == `abort);

    return [ret, settings];


}
