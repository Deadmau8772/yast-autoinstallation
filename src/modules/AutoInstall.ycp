/**
 * File:	modules/AutoInstall.ycp
 * Package:	Auto-installation
 * Summary:	Auto-installation related functions module
 * Author:	Anas Nashif <nashif@suse.de>
 *
 * $Id$
 */

{

    module "AutoInstall";
    textdomain "autoinst";

    import "Profile";
    import "Mode";
    import "AutoinstConfig";
    import "Report";


    //
    // Parsed data from XML control in YCP format
    //
    string parsedControlFile = AutoinstConfig::cache + "/autoinst.ycp";
    

    //
    // Auto-Configuration mode
    //
    global boolean autoconf = false;


    
    /**
     * Read saved data in continue mode
     * @return boolean true on success
     */
    global define boolean Continue() ``{

	//
	// First check if there are some other control files availabe
	// i.e. for post-installation only
	//
	string autoconf_file = AutoinstConfig::runtime_dir + "/autoconf/autoconf.xml";

	if ( SCR::Read(.target.size, autoconf_file) != -1 )
	{
	    y2milestone("XML Post installation data found: %1", autoconf_file );
	    autoconf = true;
	    Profile::Read(  autoconf_file  );
	    boolean ret = Profile::Read(  AutoinstConfig::xml_tmpfile );
	    SCR::Execute(.target.bash, sformat("/bin/mv %1 %2", autoconf_file, AutoinstConfig::cache));
	   
	    return (ret);
	}
	else
	{
	    boolean ret = Profile::ReadYCP ( parsedControlFile );
	    if ( Profile::current == $[] || !ret )	
	    {
		y2milestone( "No saved autoinstall data found" );
		return (false);
	    }
	    else
	    {
		y2milestone( "Found and read saved autoinst data : <%1>", Profile::current );
		SCR::Execute(.target.remove, parsedControlFile);
		
		return (true);
	    }
	}

    }

    /**
     * Constructer
     * @return: void
     */
    global define void AutoInstall()
	``{
	if (!Mode::config &&  ! ( Mode::cont && Continue() ))
	{
	    if ( SCR::Read(.target.size, AutoinstConfig::xml_tmpfile) != -1  && size ( Profile::current ) == 0)
	    {
		//
		// Profile is available and it has not been parsed yet.
		//
		Profile::Read(  AutoinstConfig::xml_tmpfile );
		Report::Import(Profile::Flat["report"]:$[]);
	    }
	}

	if (Mode::cont && size ( Profile::current ) > 0)
	{
	    y2milestone("Enabling Auto-Installation mode");
	    Mode::autoinst = true;
	    Report::Import(Profile::Flat["report"]:$[]);
	}


	return;
    }


    /**
     * Save configuration
     * @return: boolean true on success
     */

    global define boolean Save()
	``{
	return (Profile::SaveYCP( parsedControlFile ));
    }



}

