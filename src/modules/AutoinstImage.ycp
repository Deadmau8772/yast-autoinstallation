/**
 * File:	modules/AutoinstImage.ycp
 * Package:	Auto-installation
 * Summary:	Process Auto-Installation Images
 * Author:	Uwe Gansert <uwe.gansert@suse.de>
 *
 * $Id$
 */
{

    module "AutoinstImage";
    textdomain "autoinst";

    import "Progress";
    import "AutoinstConfig";
    import "URL";
    import "AutoinstSoftware";

    include "autoinstall/io.ycp";

    global define boolean getScript() {
        boolean ret = false;
        if( AutoinstSoftware::image["script_location"]:"" != "" ) {
            map urltok = $[];

            urltok = URL::Parse (AutoinstSoftware::image["script_location"]:"");
            string scheme      = urltok["scheme"]:"default";
            string host        = urltok["host"]:"";
            string filepath    = urltok["path"]:"";
            ret = Get(scheme,host,filepath, "/tmp/image.sh");

        } else if( AutoinstSoftware::image["script_source"]:"" != "" ) {
            SCR::Write(.target.string,  "/tmp/image.sh", AutoinstSoftware::image["script_source"]:"");
            ret = true;
        }
        return ret;
    }

    global define integer runScript() {
        string params = mergestring( AutoinstSoftware::image["script_params"]:[], " " );
        return (integer)SCR::Execute(.target.bash, sformat( "/bin/sh -x /tmp/image.sh %1 > /tmp/ayast_image.log 2>&1",params) );
    }
}
