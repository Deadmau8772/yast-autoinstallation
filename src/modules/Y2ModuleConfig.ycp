/**
 * File:	modules/Y2ModuleConfig.ycp
 * Package:	Auto-installation
 * Summary:	Read data from desktop files
 * Author:	Anas Nashif <nashif@suse.de>
 *
 * $Id$
 */

{
    module "Y2ModuleConfig";
    textdomain "autoinst";

    import "Mode";
    import "Profile";
    import "Installation";
    
    include "autoinstall/io.ycp";
   

    global map GroupMap = $[];
    global map ModuleMap = $[];

    /**
     * MenuTreeData
     * @return: list of modules
     */
    global  list MenuTreeData =[];


    global boolean x11_installed = false;

    /**
     * Read module configuration files
     * @return: map
     */
    global define list ReadMenuEntries()
	``{
	import "Desktop";
	Desktop::AgentPath = .autoyast2.desktop;

	list Values= [ "Name",
		  "GenericName",
		  "X-SuSE-YaST-AutoInst",
		  "X-SuSE-YaST-AutoinstResource",
		  "X-SuSE-YaST-AutoinstClient",
		  "X-SuSE-YaST-Group",
		  "X-SuSE-YaST-AutoinstPath",
		  "X-SuSE-YaST-AutoinstMerge",
		  "X-SuSE-YaST-AutoinstMergeTypes"
	];
	Desktop::Read(Values);
	map configurations = Desktop::Modules;
	map groups = Desktop::Groups;

	map confs = $[];

	foreach (string name , map values, configurations, ``{
	    if (values["X-SuSE-YaST-AutoInst"]:"" == "configure" || values["X-SuSE-YaST-AutoInst"]:"" == "all")
	    {
		string module_auto = "";
		//
		// determine name of client, if not default name
		//
		if (!haskey(values,"X-SuSE-YaST-AutoinstClient"))
		{
		    string client =  name + "_auto";
		    values["X-SuSE-YaST-AutoinstClient"] = client;
		}          
		confs[name]=values;
	    }	    
	});
	return [confs, groups];
    }



    define list SortGroups (map GroupMap, list GroupList)
	``{
        return
	    sort (`x, `y, GroupList,
		  ``{
		string first = GroupMap[x, "SortKey"]:"";
		string second = GroupMap[y, "SortKey"]:"";
		return (first <= second);
	    });
    }


    define void CreateGroupTree (map Groups)
	``{
	y2milestone("Groups: %1", Groups);
        term groupwidget = `VBox ();
        list grouplist = [];
        grouplist = SortGroups (Groups, maplist (string rawname, map group, Groups, ``(rawname)));
        
        foreach (string name, grouplist,
		 ``{	    	   
	    map MeunTreeEntry = $["entry":name, "title":name];
	    MenuTreeData = add(MenuTreeData,MeunTreeEntry);
        });

	y2milestone("data: %1", MenuTreeData);
	return;
    }
    

    define list ConstructMenu() ``{
	
	CreateGroupTree(GroupMap);
	
	foreach(string m, map v, ModuleMap, ``{	    
	    string name = v["Name"]:"";	    
	    map menu_entry = $["entry":m, "title":name];
	    list menu_list = [];
	    if (haskey(v,"X-SuSE-YaST-Group")) {
		string parent = v["X-SuSE-YaST-Group"]:"";
		MenuTreeData = maplist (map k, MenuTreeData, ``{
		    if (k["entry"]:"" == parent) {
			list children = k["children"]:[];
			children = add(children,menu_entry);
			k["children"] = children;
			return(k);
		    }
		    else {
			return (k);
		    }
		});
	    } else {
		MenuTreeData = add(MenuTreeData,menu_entry);
	    }
	});
    }

    
    /**
     * Constructor
     */
    global define void Y2ModuleConfig () ``{

	//
	// Read module configuration data (desktop files)
	//
	
	if (!Mode::initial)
	{
	    y2debug("Modules: %1", ModuleMap);	    
	    list MenuEntries = ReadMenuEntries();
	    
	    ModuleMap = MenuEntries[0]:$[];
	    
	    Profile::ModuleMap = ModuleMap;
	    
	    GroupMap = MenuEntries[1]:$[];

	}

	
	if (Mode::config)
	{
	    // construct the tree menu
	    ConstructMenu();
	}
    }


    /**
     * Get resource name
     */
    global define string getResource( string default_resource)
	``{	
	string ret = ModuleMap[default_resource, "X-SuSE-YaST-AutoinstResource"]:"";
	if (ret == "")
	    return default_resource;
	else
	    return ret;
    }




    /**
     * Get resource data
     */
    global define any getResourceData( map resourceMap, string resource) ``{

	string tmp_resource = resourceMap["X-SuSE-YaST-AutoinstResource"]:"";
	if (tmp_resource != "")
	    resource = tmp_resource;

	string data_type = resourceMap["X-SuSE-YaST-AutoinstDataType"]:"map";
	string tomerge = resourceMap["X-SuSE-YaST-AutoinstMerge"]:"";
	string tomergetypes = resourceMap["X-SuSE-YaST-AutoinstMergeTypes"]:"";

	map mergedResource = $[];
	if (size(tomerge) > 0 )
	{
	    list MergeTypes = splitstring(tomergetypes, ",");
	    list Merge = splitstring(tomerge, ",");
	    integer i = 0;
	    foreach ( string res, Merge, ``{
		if ( MergeTypes[i]:"map" == "map")
		    mergedResource[res] = eval(Profile::Flat[res]:$[]);
		else
		    mergedResource[res] = eval(Profile::Flat[res]:[]);
		i = i + 1;
	    });
	    return mergedResource;
	}
	else
	{
	    if (data_type == "map")
		return (eval(Profile::Flat[resource]:$[]));
	    else
		return (eval(Profile::Flat[resource]:[]));
	}

    }






    /**
     * Simple dependency resolving 
     */ 
    global define list Deps () ``{

	map deps = $[
			"files": ["users"],
			"nfs":	 ["lan"],
			"nfs_server": ["lan"],
			"samba-server": ["lan"],
			"samba-client": ["lan"],
			"printer": ["lan"],
			"firewall": ["lan"],
			];

	list m = [];
	list done = [];
	
	foreach(string p, map d, ModuleMap,
        ``{

		if (!contains(done, p)) {
		if (haskey(deps, p)) {
			y2milestone("found dep");
			foreach(list r, deps[p]:[], ``{
				if (!contains(done, r)) {
				y2milestone("working on %1", r);
				m = add(m, $["res":r, "data":ModuleMap[r]:$[]]);
				done = add(done, r);
				}
			});
			m = add(m, $["res":p, "data":d]);
			done = add(done, p);
		}
		else {
			m = add(m, $["res":p, "data":d]);
			done = add(done, p);
		}
		}
	});

	return m;

    }

}
