/**
 * File:	modules/AutoinstSoftware.ycp
 * Package:	Autoyast
 * Summary:	Software
 * Authors:	Anas Nashif <nashif@suse.de>
 *
 * $Id$
 *
 */
{
    module "AutoinstSoftware";
    textdomain "autoinst";

    import "Summary";
    import "Mode";


    global map Software = $[];

    global string base = "";
    global list addons = [];
    global list packages = [];
    global string kernel = "";
    global list post_packages = [];

    global list forcedPackages = [];

    global boolean showErrors = true;

    global define void AutoinstSoftware() ``{
	if ( Mode::cont )
	{
	    import "Profile";
	    Import(Profile::current["software"]:$[]);
	}

    }

    /**
     * Import data
     * @param settings settings to be imported
     * @return true on success
     */
    global define boolean Import(map settings) ``{

	Software = settings;
	base = settings["base"]:"";
	addons = settings["addons"]:[];
	packages = settings["packages"]:[];
	kernel = settings["kernel"]:"";
	post_packages = settings["post-packages"]:[];
	showErrors = settings["show_errors"]:true;
	if (Mode::initial)
	{
	    import "AutoInstall";
	    AutoInstall::IgnorePkgErr = showErrors;
	}
	return true;
    }


    /**
     * Export data
     * @return dumped settings (later acceptable by Import())
     */
    global define map Export() ``{

	map s = $[];
	if (base != "")
	    s["base"] = base ;
	if (kernel != "")
	    s["kernel"] = kernel ;

	if (addons != [])
	    s["addons"] = addons ;

	if (packages != [])
	    s["packages"] = packages ;

	if (post_packages != [])
	    s["post-packages"] = post_packages ;

	if (!showErrors)
	    s["show_errors"] = false;

	return (s);
    }


    /**
     * Add packages needed by modules, i.e. NIS, NFS etc.
     * @param list of packages to add
     * @return void
     */
    global define void AddModulePackages(list module_packages) ``{
	packages = toset(union(packages,  module_packages));
	// Update profile
	import "Profile";
	Profile::Flat["software"] = Export();
	return;
    }

    /**
     * Remove packages not needed by modules, i.e. NIS, NFS etc.
     * @param list of packages to remove
     * @return void
     */

    global define void RemoveModulePackages (list module_packages) ``{
	packages = filter(string p, packages, ``(!contains(module_packages,p)));
	import "Profile";
	Profile::Flat["software"] = Export();
	return;
    }



    /**
     * Summary
     * @return Html formatted configuration summary
     */
    global define string Summary()``{
	string summary = "";
	summary = Summary::AddHeader(summary, _("Base Selection"));
	summary = Summary::AddLine(summary, (base != "") ?
				   base : Summary::NotConfigured());
	summary = Summary::AddHeader(summary, _("Add-on Selections"));
	if (size( addons) > 0 )
	{
	    summary = Summary::OpenList(summary);
	    foreach(`a, addons, ``{
		summary = Summary::AddListItem(summary, a);
	    });
	    summary = Summary::CloseList(summary);
	}
	else
	{
	    summary = Summary::AddLine(summary, Summary::NotConfigured());
	}
	summary = Summary::AddHeader(summary, _("Individually Selected Packages"));
	summary = Summary::AddLine(summary, sformat("%1", size(packages)));

	return summary;
    }



    /**
     * Configure software settings
     * @param void
     * @return boolean
     */
    global define boolean Write()
	``{
	boolean ok = true;
	boolean ret = false;

	// y2milestone("set Locale to %1", Language::language);
	Pkg::SetLocale (Language::language);

	Packages::Init(true);
	// Standard Packages
	boolean retval = false;

	string  base_selection = base;
	if (! Pkg::SetSelection( base_selection ))
	{
	    y2error("Error while setting base selection: %1", base_selection);
	    Report::Error(sformat(_("Could not set base selection: %1"), base_selection));
	    return false;
	}

	list failed = [];
	foreach(`s, toset(addons),
		``{
	    if (!Pkg::SetSelection( s ))
	    {

		failed = add(failed, s);

	    }
	});

	list failed_again = [];
	if (size(failed) > 0 ) {

	    foreach(`s, failed,
		    ``{
		if (!Pkg::SetSelection( s ))
		{
		    failed_again = add(failed_again, s);
		}
	    });
	}
	if (size(failed_again) > 0 )
	{
	    y2error("Error while setting-addon selection: %1", s);
	    Report::Warning(_("Could not set add-on selections."));
	}


	// single selected packages

	if (!Pkg::ActivateSelections())
	{
	    return false;
	}

	list extraPackages = calcExtraPackages ();
	y2debug("Packages selected in autoinstall mode: %1", extraPackages);

	if (size(extraPackages) > 0 )
	{
	    Pkg::DoProvide(extraPackages);
	}

	Pkg::DoProvide (Packages::ComputeSystemPackageList());
	Pkg::PkgSolve();


	SpaceCalculation::ShowPartitionWarning();
	y2milestone( "INSTALLING Software Selection: base(%1), addon(%2)",  Pkg::GetSelections(`selected, "base"), Pkg::GetSelections(`selected, ""));

	return (ok);
    }


    global define list calcExtraPackages ()
	``{

	list extra_packages = union( packages , forcedPackages);

	if (kernel != "")
	{
	    extra_packages = add ( extra_packages , kernel);
	}
	return extra_packages;
    }







    global define list autoinstPackages()
	``{
	import "Profile";

	list allpackages = [];

	allpackages = union (allpackages, packages);

	// In autoinst mode, a kernel might be external and not available
	// in packages
	if (  size(kernel) == 0)
	{
	    import "Kernel";
	    allpackages = union (allpackages, Kernel::kernel_packages);
	}
	else {
	    allpackages = add (allpackages, kernel);
	}

	if (haskey(Profile::Flat, "nis")) {
		map nis = Profile::Flat["nis"]:$[];
		if (nis["start_nis"]:false)
			allpackages = add(allpackages, "ypbind");
		if (nis["start_autofs"]:false)
			allpackages = add(allpackages, "autofs");
	}

	if (haskey(Profile::Flat, "ldap")) {
		map ldapclient = Profile::Flat["ldap"]:$[];
		if (ldapclient["start_ldap"]:false)
			allpackages = union(allpackages, ["pam_ldap","nss_ldap"]);
	}

	if (haskey(Profile::Flat, "nfs_server"))
	{
	    boolean start_nfsserver = Profile::Flat["nfs_server", "start_nfsserver"]:false;
	    if (start_nfsserver)
		allpackages = union(allpackages, ["yast2-nfs-server", "nfs-utils"]);
	}


	if (haskey(Profile::Flat, "nisplus")) {
		map nisplus = Profile::Flat["nisplus"]:$[];
		if (nisplus["start_nisplus"]:false)
			allpackages = add(allpackages, "nis-utils");
	}
	if (haskey(Profile::Flat, "mail")) {
		map mail = Profile::Flat["mail"]:$[];
		if (mail["mta"]:`other == `postfix)
		{
			allpackages = add(allpackages, "postfix");
			if (mail["use_amavis"]:false)
				allpackages = add(allpackages, "amavis-postfix");
		}
		else if (mail["mta"]:`other == `sendmail)
		{
			allpackages = add(allpackages, "sendmail");
			if (mail["use_amavis"]:false)
				allpackages = add(allpackages, "amavis-sendmail");
		}
	}
	list extraPackages = calcExtraPackages ();
	if (size(extraPackages) > 0 )
	{
	    allpackages = union(allpackages,extraPackages);
	}
	y2debug("package list calculated for auto-installation: %1", allpackages);
	return allpackages;
    }

    //EOF
}
